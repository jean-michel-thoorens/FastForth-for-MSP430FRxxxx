; macro called by SendSource.bat
; param1 = this macro
; param2 = %~dpn1.4TH to write in SD_CARD
; param3 = /C = default COMx saved in TERATERM.INI.
; param4 = %deviceID% from select.bat

basename fname param2       ; fname = file.ext of param2
strinsert fname 1 '\'       ; fname = \file.ext, to force absolute path

connect  param3

;logfile = '\teraterm.log'
;dirname logdir param2  ; logir = dir of file.4TH to be sent
;strconcat logdir logfile
;logopen logdir 0


inputbox 'here you can change path of the file: ' 'Send this file to SD_CARD target' fname

setecho 0           ; no echo from Teraterm
showtt 0            ; close teraterm windows

; DEVICE ID TEST
; ==============

sendln 'CODE ?ID';                              CODE ?ID
sendln 'CMP #0,R14 0<> IF SUB &$1A04,R14 THEN'; CMP #0,TOS 0<> IF SUB &DEVICEID,TOS THEN
sendln 'COLON ';                                COLON
sendln '$1B EMIT $63 EMIT';                     $1B EMIT $63 EMIT   \ send 'ESC c' (clear screen)
sendln 'CR ABORT" Device'39's ID mismatch!" ' ; CR ABORT" Device's ID mismatch!"
sendln 'PWR_STATE ;' ;                          PWR_STATE ;

sendln param4 ' ?ID NOECHO'    ; send: %deviceID% ?ID

sendln 'TERM2SD" ' inputstr '"'    ; send to FastForth the command TERM2SD" \file.4TH" ...with optionnal path added in inputbox

uptime timestart ; starts chrono...

sendfile param2 0       ; binary flag = 0 to send text file

sendln #4               ; send ETX to ask TERM2SD" to close file

uptime timeend  ; stops chrono...
diff = timeend - timestart

sendln 'ECHO'

showtt 1    ; display teraterm window

sprintf2 resultat " %s download to SD Card: %d msec" param2 diff
messagebox resultat "teraterm.exe"


;logclose

unlink
